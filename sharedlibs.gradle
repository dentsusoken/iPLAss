ext {
	//for version management
	sharedLibs = [
		// Jakarta EE 10 apis
		// https://projects.eclipse.org/releases/jakarta-10

		// Jakarta Mail
		// https://projects.eclipse.org/projects/ee4j.mail
		'jakarta.mail:jakarta.mail-api': '2.1.3',

		// Jakarta Validation
		// https://projects.eclipse.org/projects/ee4j.validation
		'jakarta.validation:jakarta.validation-api': '3.0.2',

		// Jakarta EL
		// https://projects.eclipse.org/projects/ee4j.el
		// NOTE 5.0.1 が存在しているが、実装がない
		'jakarta.el:jakarta.el-api': '5.0.0',

		// Jakarta Activation
		// https://projects.eclipse.org/projects/ee4j.jaf
		'jakarta.activation:jakarta.activation-api': '2.1.3',

		// Jakarta xml binding
		// https://jakarta.ee/specifications/xml-binding/
		// https://jakarta.ee/specifications/xml-binding/4.0/
		// https://projects.eclipse.org/projects/ee4j.jaxb
		'jakarta.xml.bind:jakarta.xml.bind-api': '4.0.2',

		// Jakarta Servlet
		// https://projects.eclipse.org/projects/ee4j.servlet
		'jakarta.servlet:jakarta.servlet-api': '6.0.0',

		// Jakarta JSP
		// https://projects.eclipse.org/projects/ee4j.jsp
		'jakarta.servlet.jsp:jakarta.servlet.jsp-api': '3.1.1',

		// Jakarta jstl
		// https://projects.eclipse.org/projects/ee4j.jstl
		'jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api': '3.0.0',

		// Jakarta rest
		// https://projects.eclipse.org/projects/ee4j.rest
		'jakarta.ws.rs:jakarta.ws.rs-api': '3.1.0',

		// Jakarta Annotations
		// https://projects.eclipse.org/projects/ee4j.ca
		'jakarta.annotation:jakarta.annotation-api': '2.1.1',
		
		// Jakarta EE 10 runtimes
		// Jakarta Mail
		// https://eclipse-ee4j.github.io/angus-mail/
		'org.eclipse.angus:angus-mail': '2.0.3',

		// Jakarta Validation
		// https://hibernate.org/validator/releases/8.0/
		'org.hibernate.validator:hibernate-validator': '8.0.1.Final',

		// Jakarta EL
		// https://jakarta.ee/specifications/expression-language/5.0/
		// https://github.com/eclipse-ee4j/expressly
		// https://projects.eclipse.org/projects/ee4j.expressly
		// NOTE: incubating
		'org.glassfish.expressly:expressly': '5.0.0',

		// Jakarta Activation
		// https://eclipse-ee4j.github.io/angus-activation/
		'org.eclipse.angus:angus-activation': '2.0.2',

		// Jakarta xml binding
		// https://eclipse-ee4j.github.io/jaxb-ri/
		// NOTE: 公式には、 `com.sun.xml.bind:jaxb-impl` となっている。実体は org.glassfish.jaxb 。
		'org.glassfish.jaxb:jaxb-runtime': '4.0.5',

		// Jakarta Servlet
		// apache tomcat bundled
		
		// Jakarta JSP - tomcat bundled
		// apache tomcat bundled
		// for jsp compilation
		'org.apache.tomcat:tomcat-jasper': '10.1.26',

		// Jakarta jstl
		'org.glassfish.web:jakarta.servlet.jsp.jstl': '3.0.1',

		// Jakarta rest
		// https://eclipse-ee4j.github.io/jersey/
		'org.glassfish.jersey:jersey-bom': '3.1.7',//by bom

		// Jakarta Annotations
		// apache tomcat bundled

		//3rd party libs
		//apache commons
		'org.apache.commons:commons-lang3': '3.14.0',
		'org.apache.commons:commons-text': '1.12.0',
		'org.apache.commons:commons-collections4': '4.4',
		'org.apache.commons:commons-pool2': '2.12.0',
		'commons-io:commons-io': '2.16.1',
		'commons-beanutils:commons-beanutils': '1.9.4',
		'commons-codec:commons-codec': '1.17.0',
		
		'org.apache.httpcomponents.client5:httpclient5': '5.3.1',
		'org.apache.httpcomponents.core5:httpcore5': '5.2.5',
		'org.apache.httpcomponents.core5:httpcore5-h2': '5.2.5',
		
		//lucene
		'org.apache.lucene:lucene-queryparser': '9.11.1',
		'org.apache.lucene:lucene-analysis-kuromoji': '9.11.1',
		'org.apache.lucene:lucene-backward-codecs': '9.11.1',
		
		//tika
		'org.apache.tika:tika-core': '2.9.2',
		'org.apache.tika:tika-parser-html-module': '2.9.2',
		'org.apache.tika:tika-parser-xml-module': '2.9.2',
		'org.apache.tika:tika-parser-microsoft-module': '2.9.2',
		'org.apache.tika:tika-parser-pdf-module': '2.9.2',
		'org.apache.tika:tika-parser-zip-commons': '2.9.2',
		// commons-compress は tika に関連した競合ライブラリの為、個別定義しバージョンを管理
		'org.apache.commons:commons-compress': '1.26.2',
		
		'org.apache.poi:poi': '5.3.0',
		'org.apache.poi:poi-scratchpad': '5.3.0',
		'org.apache.poi:poi-ooxml': '5.3.0',
		
		'org.jxls:jxls': '3.0.0',
		'org.jxls:jxls-poi': '3.0.0',
		
		'org.apache.groovy:groovy-bom': '4.0.21', //by bom
		
		'com.fasterxml.jackson:jackson-bom': '2.17.2',//by bom
		
		'net.sf.supercsv:super-csv': '2.4.0',
		
		'com.github.ben-manes.caffeine:caffeine': '3.1.8',
		
		'org.slf4j:slf4j-api': '2.0.13',
		'org.slf4j:jcl-over-slf4j': '2.0.13',
		'org.slf4j:log4j-over-slf4j': '2.0.13',
		'org.apache.logging.log4j:log4j-to-slf4j': '2.23.1',
		'ch.qos.logback:logback-classic': '1.5.6',
		'net.logstash.logback:logstash-logback-encoder': '7.4',
		
		'org.bouncycastle:bcjmail-jdk18on': '1.78.1',
		
		'org.jgroups:jgroups': '5.3.6.Final',

		// gwt
		// https://github.com/jiakuan/gwt-gradle-plugin
		// https://github.com/jiakuan/gwt-gradle-plugin/blob/main/src/main/java/org/docstr/gradle/plugins/gwt/GwtBasePlugin.java
		'org.gwtproject:gwt-dev': '2.11.0',
		'org.gwtproject:gwt-user': '2.11.0',
		'org.gwtproject:gwt-servlet-jakarta': '2.11.0',
		'org.gwtproject:gwt-codeserver': '2.11.0',

		'org.infinispan:infinispan-core': '15.0.4.Final',
		
		'io.lettuce:lettuce-core': '6.3.2.RELEASE',
		
		'net.sf.jasperreports:jasperreports': '6.21.3',
		'com.github.librepdf:openpdf': '2.0.2',

		// FIXME version is miliestone
		'org.apache.commons:commons-fileupload2-jakarta-servlet6': '2.0.0-M2',
		'io.jsonwebtoken:jjwt-api': '0.11.5',
		'io.jsonwebtoken:jjwt-impl': '0.11.5',
		'io.jsonwebtoken:jjwt-jackson': '0.11.5',
		
		'com.ibm.icu:icu4j': '75.1',
		
		'org.junit:junit-bom': '5.10.2',
		
		//webjars(admin)
		'org.webjars.npm:ace-builds': '1.35.3',
		
		//webjars(gem)
		'org.webjars:jquery': '3.7.1',
		'org.webjars:pdf-js': '4.4.168',
		'org.webjars:ckeditor': '4.22.1',
		'org.webjars:jquery-blockui': '2.70-1',
		'org.webjars:jQuery-Timepicker-Addon': '1.6.3',

		'org.webjars.npm:moment': '2.30.1',

		'org.webjars.npm:free-jqgrid': '4.15.5',
		'org.webjars.npm:jqtree': '1.4.9',
		'org.webjars.npm:viewerjs': '1.11.1',

		'org.webjars.npm:blueimp-file-upload': '10.32.0',
		'org.webjars.npm:fullcalendar': '3.10.5',
		'org.webjars.npm:mediaelement': '4.2.17',

		'org.webjars.npm:jquery-ui': '1.13.3',

		'org.webjars.npm:quill': '2.0.2',

		'org.webjars:font-awesome': '5.13.1',
	]
	
	sharedLib = { String name ->
		return name + ':' + sharedLibs[name]
	}
	
	//globally exclude libraries
	globalExcludes = [
		//use jcl-over-slf4j
		['group': "commons-logging", module: 'commons-logging']
	]
}

configurations {
	// jakarta api definition
	jakartaApiMail
	jakartaApiValidation
	jakartaApiEL
	jakartaApiActivation
	jakartaApiXMLBinding
	jakartaApiServlet
	jakartaApiJSP
	jakartaApiJstl
	jakartaApiRest
	jakartaApiAnnotations

	// jakarta impl definition
	jakartaImplMail
	jakartaImplValidation
	jakartaImplEL
	jakartaImplActivation
	jakartaImplXMLBinding
	//jakartaImplServlet
	jakartaImplJSP
	jakartaImplJstl
	jakartaImplJstlTransitive
	jakartaImplRest
	//jakartaImplAnnotations

	// provided Jakarta EE 10 apis
	jeecoreapis
	jeecoreapis.extendsFrom jakartaApiMail
	jeecoreapis.extendsFrom jakartaApiValidation
	jeecoreapis.extendsFrom jakartaApiEL
	jeecoreapis.extendsFrom jakartaApiActivation
	jeecoreapis.extendsFrom jakartaApiXMLBinding
	
	
	// provided Jakarta EE 10 web apis
	jeewebapis
	jeewebapis.extendsFrom jakartaApiServlet
	jeewebapis.extendsFrom jakartaApiJSP
	jeewebapis.extendsFrom jakartaApiEL
	jeewebapis.extendsFrom jakartaApiJstl
	jeewebapis.extendsFrom jakartaImplJstlTransitive
	jeewebapis.extendsFrom jakartaApiRest
	jeewebapis.extendsFrom jakartaApiAnnotations
	
	// Jakarta EE 10 runtime for tomcat10.1
	jeeruntime4tomcat
	jeeruntime4tomcat.extendsFrom jakartaApiJstl
	jeeruntime4tomcat.extendsFrom jakartaImplJstl
	jeeruntime4tomcat.extendsFrom jakartaImplMail
	jeeruntime4tomcat.extendsFrom jakartaImplValidation
	jeeruntime4tomcat.extendsFrom jakartaImplEL
	jeeruntime4tomcat.extendsFrom jakartaImplRest
	jeeruntime4tomcat.extendsFrom jakartaApiXMLBinding
	jeeruntime4tomcat.extendsFrom jakartaImplXMLBinding
	jeeruntime4tomcat.extendsFrom jakartaImplActivation
}

dependencies {
	// all dependencies
	implementation platform(sharedLib('org.apache.groovy:groovy-bom'))
	implementation platform(sharedLib('com.fasterxml.jackson:jackson-bom'))
	
	// jakarta api definition
	jakartaApiMail sharedLib('jakarta.mail:jakarta.mail-api')
	jakartaApiValidation sharedLib('jakarta.validation:jakarta.validation-api')
	jakartaApiEL sharedLib('jakarta.el:jakarta.el-api')
	jakartaApiActivation sharedLib('jakarta.activation:jakarta.activation-api')
	jakartaApiXMLBinding sharedLib('jakarta.xml.bind:jakarta.xml.bind-api')
	jakartaApiServlet sharedLib('jakarta.servlet:jakarta.servlet-api')
	jakartaApiJSP sharedLib('jakarta.servlet.jsp:jakarta.servlet.jsp-api')
	jakartaApiJstl sharedLib('jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api')
	jakartaApiRest sharedLib('jakarta.ws.rs:jakarta.ws.rs-api')
	jakartaApiAnnotations sharedLib('jakarta.annotation:jakarta.annotation-api')

	// jakarta impl definition
	jakartaImplMail sharedLib('org.eclipse.angus:angus-mail')
	jakartaImplValidation sharedLib('org.hibernate.validator:hibernate-validator')
	jakartaImplEL sharedLib('org.glassfish.expressly:expressly')
	jakartaImplActivation sharedLib('org.eclipse.angus:angus-activation')
	jakartaImplXMLBinding sharedLib('org.glassfish.jaxb:jaxb-runtime')
	//jakartaImplServlet
	jakartaImplJSP sharedLib('org.apache.tomcat:tomcat-jasper')
	jakartaImplJstl sharedLib('org.glassfish.web:jakarta.servlet.jsp.jstl')
	jakartaImplJstlTransitive(sharedLib('org.glassfish.web:jakarta.servlet.jsp.jstl')) {
		transitive = false
	}
	jakartaImplRest platform(sharedLib('org.glassfish.jersey:jersey-bom'))
	jakartaImplRest 'org.glassfish.jersey.containers:jersey-container-servlet'
	jakartaImplRest 'org.glassfish.jersey.media:jersey-media-jaxb'
	jakartaImplRest 'org.glassfish.jersey.media:jersey-media-json-jackson'
	jakartaImplRest 'org.glassfish.jersey.media:jersey-media-multipart'
	jakartaImplRest 'org.glassfish.jersey.inject:jersey-hk2'
	jakartaImplRest 'com.fasterxml.jackson.jakarta.rs:jackson-jakarta-rs-json-provider'
	//jakartaImplAnnotations


	// Jakarta EE 10 runtime for tomcat10.1
	// jackson management
	jeeruntime4tomcat platform(sharedLib('com.fasterxml.jackson:jackson-bom'))
}
