ext {
	//for version management
	sharedLibs = [
		// Jakarta 10 apis
		// https://projects.eclipse.org/releases/jakarta-10

		// https://projects.eclipse.org/projects/ee4j.mail
		'jakarta.mail:jakarta.mail-api': '2.1.3',
		// https://projects.eclipse.org/projects/ee4j.validation
		'jakarta.validation:jakarta.validation-api': '3.0.2',
		// https://projects.eclipse.org/projects/ee4j.el
		// NOTE 5.0.1 が存在しているが、実装がない
		'jakarta.el:jakarta.el-api': '5.0.0',
		// https://projects.eclipse.org/projects/ee4j.jaf
		'jakarta.activation:jakarta.activation-api': '2.1.3',
		// https://jakarta.ee/specifications/xml-binding/
		// https://jakarta.ee/specifications/xml-binding/4.0/
		// https://projects.eclipse.org/projects/ee4j.jaxb
		'jakarta.xml.bind:jakarta.xml.bind-api': '4.0.2',

		// https://projects.eclipse.org/projects/ee4j.servlet
		'jakarta.servlet:jakarta.servlet-api': '6.0.0',
		// https://projects.eclipse.org/projects/ee4j.jsp
		'jakarta.servlet.jsp:jakarta.servlet.jsp-api': '3.1.1',
		// https://projects.eclipse.org/projects/ee4j.jstl
		'jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api': '3.0.0',
		// https://projects.eclipse.org/projects/ee4j.rest
		'jakarta.ws.rs:jakarta.ws.rs-api': '3.1.0', // TODO jakartaee 10 では  4.0.0 とあるが、リリースされていない。
		// https://projects.eclipse.org/projects/ee4j.ca
		'jakarta.annotation:jakarta.annotation-api': '2.1.1',
		
		// Jakarta 10 runtimes
		// https://eclipse-ee4j.github.io/angus-mail/
		'org.eclipse.angus:angus-mail': '2.0.3',
		// https://hibernate.org/validator/releases/8.0/
		'org.hibernate.validator:hibernate-validator': '8.0.1.Final',
		// https://jakarta.ee/specifications/expression-language/5.0/
		// https://github.com/eclipse-ee4j/expressly
		// https://projects.eclipse.org/projects/ee4j.expressly
		// NOTE: incubating
		'org.glassfish.expressly:expressly': '5.0.0',
		// https://eclipse-ee4j.github.io/angus-activation/
		'org.eclipse.angus:angus-activation': '2.0.2',
		// https://eclipse-ee4j.github.io/jaxb-ri/
		// NOTE: 公式には、 `com.sun.xml.bind:jaxb-impl` となっている。実体は org.glassfish.jaxb 。
		'org.glassfish.jaxb:jaxb-runtime': '4.0.5',
		// https://eclipse-ee4j.github.io/jersey/
		'org.glassfish.jersey:jersey-bom': '3.1.5',//by bom

		//3rd party libs
		//apache commons
		'org.apache.commons:commons-lang3': '3.14.0',
		'org.apache.commons:commons-text': '1.2',
		'org.apache.commons:commons-collections4': '4.4',
		'org.apache.commons:commons-pool2': '2.11.1',
		'commons-io:commons-io': '2.15.1',
		'commons-beanutils:commons-beanutils': '1.9.4',
		'commons-codec:commons-codec': '1.15',
		
		'org.apache.httpcomponents:httpclient': '4.5.13',
		'org.apache.httpcomponents:httpmime': '4.5.13',
		
		//lucene
		'org.apache.lucene:lucene-queryparser': '8.11.2',
		'org.apache.lucene:lucene-analyzers-kuromoji': '8.11.2',
		'org.apache.lucene:lucene-analyzers-smartcn': '8.11.2',
		'org.apache.lucene:lucene-backward-codecs': '8.11.2',
		'org.apache.lucene:lucene-highlighter': '8.11.2',
		
		//tika(依存関係多いので明示的に指定する)
		'org.apache.tika:tika-core': '1.28.5',
		'org.apache.tika:tika-parsers': '1.28.5',
		'org.apache.commons:commons-compress': '1.26.0',
		'org.apache.pdfbox:jempbox': '1.8.16',
		'org.apache.pdfbox:pdfbox': '2.0.24',
		'org.apache.pdfbox:pdfbox-tools': '2.0.24',
		'org.ccil.cowan.tagsoup:tagsoup': '1.2.1',
		// tika の依存関係。pptx 解析に必要。org.tallison:metadata-extractor は fork プロジェクトなので、バージョンアップ時にグループが変わらないか注意
		'org.tallison:metadata-extractor': '2.17.1.0',
		
		'org.apache.poi:poi': '5.2.3',
		'org.apache.poi:poi-scratchpad': '5.2.3',
		'org.apache.poi:poi-ooxml': '5.2.3',
		
		'org.jxls:jxls': '2.14.0',
		'org.jxls:jxls-poi': '2.14.0',
		
		'org.codehaus.groovy:groovy-bom': '3.0.12',//by bom
		
		'com.fasterxml.jackson:jackson-bom': '2.15.2',//by bom
		
		'net.sf.supercsv:super-csv': '2.4.0',
		
		'com.github.ben-manes.caffeine:caffeine': '2.9.3',
		
		'org.slf4j:slf4j-api': '1.7.36',
		'org.slf4j:jcl-over-slf4j': '1.7.36',
		'org.slf4j:log4j-over-slf4j': '1.7.36',
		'org.apache.logging.log4j:log4j-to-slf4j': '2.19.0',
		'ch.qos.logback:logback-classic': '1.2.13',
		'net.logstash.logback:logstash-logback-encoder': '7.0.1',
		
		'org.bouncycastle:bcjmail-jdk18on': '1.76',
		
		'org.jgroups:jgroups': '4.2.16.Final',

		// gwt
		// https://github.com/jiakuan/gwt-gradle-plugin
		// https://github.com/jiakuan/gwt-gradle-plugin/blob/main/src/main/java/org/docstr/gradle/plugins/gwt/GwtBasePlugin.java
		'org.gwtproject:gwt-dev': '2.11.0',
		'org.gwtproject:gwt-user': '2.11.0',
		'org.gwtproject:gwt-servlet-jakarta': '2.11.0',
		'org.gwtproject:gwt-codeserver': '2.11.0',

		'org.infinispan:infinispan-core': '9.4.24.Final',
		
		'io.lettuce:lettuce-core': '6.2.5.RELEASE',
		
		'net.sf.jasperreports:jasperreports': '6.20.6',
		'com.github.librepdf:openpdf': '1.3.33',

		// FIXME version is miliestone
		'org.apache.commons:commons-fileupload2-jakarta-servlet6': '2.0.0-M2',
		'io.jsonwebtoken:jjwt-api': '0.11.5',
		'io.jsonwebtoken:jjwt-impl': '0.11.5',
		'io.jsonwebtoken:jjwt-jackson': '0.11.5',
		
		'com.ibm.icu:icu4j': '69.1',
		
		
		'junit:junit': '4.13.1',
		
		//webjars(admin)
		'org.webjars.npm:ace-builds': '1.12.3',
		
		//webjars(gem)
		'org.webjars:jquery': '3.5.1',
		'org.webjars:pdf-js': '3.1.81',
		'org.webjars:ckeditor': '4.22.1',
		'org.webjars:jquery-blockui': '2.70-1',
		'org.webjars:jQuery-Timepicker-Addon': '1.6.3',
		'org.webjars:momentjs': '2.29.4',
		
		'org.webjars.npm:free-jqgrid': '4.15.5',
		'org.webjars.npm:jqtree': '1.4.9',
		'org.webjars.npm:viewerjs': '1.9.0',
		
		'org.webjars.bower:blueimp-file-upload': '10.30.1',
		'org.webjars.bower:fullcalendar': '3.10.2',
		'org.webjars.bower:mediaelement': '4.2.5',
		
		'org.webjars.bowergithub.jquery:jquery-ui': '1.13.2',
		
		'org.webjars:font-awesome': '5.13.1',
		
		//for jsp compilation
		'org.apache.tomcat:tomcat-jasper': '10.1.19',
		'org.glassfish.web:jakarta.servlet.jsp.jstl': '3.0.1'
		
	]
	
	sharedLib = { String name ->
		return name + ':' + sharedLibs[name]
	}
	
	//globally exclude libraries
	globalExcludes = [
		//use jcl-over-slf4j
		['group': "commons-logging", module: 'commons-logging']
	]
}

configurations {
	jeecoreapis
	jeewebapis
	jeeruntime4tomcat
}

dependencies {
	implementation platform(sharedLib('org.codehaus.groovy:groovy-bom'))
	implementation platform(sharedLib('com.fasterxml.jackson:jackson-bom'))
	
	//provided JavaEE(JakartaEE)9 apis
	//java mail
	jeecoreapis sharedLib('jakarta.mail:jakarta.mail-api')
	//Bean validation
	jeecoreapis sharedLib('jakarta.validation:jakarta.validation-api')
	//Expression Language
	jeecoreapis sharedLib('jakarta.el:jakarta.el-api')

	//javax activation for java11
	jeecoreapis sharedLib('jakarta.activation:jakarta.activation-api')
	//JAXB for java11
	jeecoreapis sharedLib('jakarta.xml.bind:jakarta.xml.bind-api')
	
	
	//provided JavaEE(JakartaEE)9 web apis
	//Servlet
	jeewebapis sharedLib('jakarta.servlet:jakarta.servlet-api')
	//JSP
	jeewebapis sharedLib('jakarta.servlet.jsp:jakarta.servlet.jsp-api')
	//Expression Language
	jeewebapis sharedLib('jakarta.el:jakarta.el-api')
	//jstl
	jeewebapis sharedLib('jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api')
	//for jstl tld file resolve
	jeewebapis(sharedLib('org.glassfish.web:jakarta.servlet.jsp.jstl'))	{
		transitive = false
	}
	//JAX-RS
	jeewebapis sharedLib('jakarta.ws.rs:jakarta.ws.rs-api')
	//Common Annotations for the Java Platform
	jeewebapis sharedLib('jakarta.annotation:jakarta.annotation-api')
	

	//JavaEE(JakartaEE)9 runtime for tomcat10.0

	// jackson management
	jeeruntime4tomcat platform(sharedLib('com.fasterxml.jackson:jackson-bom'))

	//jstl api
	jeeruntime4tomcat sharedLib('jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api')
	//jstl runtime
	jeeruntime4tomcat sharedLib('org.glassfish.web:jakarta.servlet.jsp.jstl')
	//java mail runtime
	jeeruntime4tomcat sharedLib('org.eclipse.angus:angus-mail')
	//Bean validation runtime
	jeeruntime4tomcat sharedLib('org.hibernate.validator:hibernate-validator')
	//EL runtime
	jeeruntime4tomcat sharedLib('org.glassfish.expressly:expressly')

	//JAX-RS runtime
	jeeruntime4tomcat platform(sharedLib('org.glassfish.jersey:jersey-bom'))
	jeeruntime4tomcat 'org.glassfish.jersey.containers:jersey-container-servlet'
	jeeruntime4tomcat 'org.glassfish.jersey.media:jersey-media-json-jackson'
	jeeruntime4tomcat 'org.glassfish.jersey.media:jersey-media-multipart'
	jeeruntime4tomcat 'org.glassfish.jersey.inject:jersey-hk2'
	jeeruntime4tomcat 'com.fasterxml.jackson.jakarta.rs:jackson-jakarta-rs-json-provider'

	//JAXB runtime for java11
	jeeruntime4tomcat sharedLib('jakarta.xml.bind:jakarta.xml.bind-api')
	jeeruntime4tomcat sharedLib('org.glassfish.jaxb:jaxb-runtime')

	//JAF runtime for java11
	jeeruntime4tomcat sharedLib('org.eclipse.angus:angus-activation')
	
}



